plugins {
  id 'org.ajoberstar.defaults' version '0.12.0'
  id 'groovy'
  id 'java-library'
  id 'java-gradle-plugin'
  id 'com.gradle.build-scan' version '1.16'
}

buildScan {
  termsOfServiceUrl   = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'
}

apply from: 'gradle/bintray.gradle'
apply from: 'gradle/locking.gradle'

group = 'org.ajoberstar'

repositories {
  jcenter()
}

dependencies {
  // gradle
  compileOnly gradleApi()

  // groovy
  compileOnly localGroovy()

  // grgit
  api 'org.ajoberstar.grgit:grgit-core:latest.release'
  compatTestImplementation 'org.ajoberstar.grgit:grgit-core:latest.release'

  // testing
  compatTestImplementation gradleTestKit()
  compatTestImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'
}

stutter {
  sparse = true
  includeActiveRc = true
  java(8) {
    compatibleRange '4.3'
  }
  java(11) {
    compatibleRange '5.0'
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/gradle-git-publish'
  vcsUrl = 'https://github.com/ajoberstar/gradle-git-publish.git'
  description = 'Publishing to Git repositories'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.git-publish'
      displayName = 'Git Publish Plugin'
      tags = ['publish', 'git']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
