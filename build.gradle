buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:semver-vcs-gradle-grgit:0.1.0'
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'org.sonarqube' version '2.2.1'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

apply plugin: 'org.ajoberstar.semver-vcs-grgit'
apply from: 'gradle/compatTest.gradle'

group = 'org.ajoberstar'

sonarqube {
    properties {
        property 'sonar.projectVersion', version.toString().split('-')[0]
    }
}

repositories {
    jcenter()
}

configurations.all {
    exclude group: 'org.codehaus.groovy'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar:grgit:1.7.3-rc.1'

    compatTestCompile gradleTestKit()
    compatTestCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    compatTestCompile 'org.ajoberstar:grgit:1.7.3-rc.1'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from tasks.javadoc.outputs.files
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        mavenLocal()
    }
}

pluginBundle {
    website = 'https://github.com/ajoberstar/gradle-git-publish'
    vcsUrl = 'https://github.com/ajoberstar/gradle-git-publish.git'
    description = 'Publishing to Git repositories'
    plugins {
        publishPlugin {
            id = 'org.ajoberstar.git-publish'
            displayName = 'Git Publish Plugin'
            tags = ['publish', 'git', 'gradle']
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

bintray {
    user = System.env['BINTRAY_USER']
    key = System.env['BINTRAY_KEY']
    publications = ['main']
    publish = true
    pkg {
        repo = 'maven'
        name = 'gradle-git-publish'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/ajoberstar/gradle-git-publish'
        issueTrackerUrl = 'https://github.com/ajoberstar/gradle-git-publish/issues'
        vcsUrl = 'https://github.com/ajoberstar/gradle-git-publish.git'
        labels = ['publish', 'git', 'gradle']
        publicDownloadNumbers = true
        githubRepo = 'ajoberstar/gradle-git-publish'

        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

wrapper {
    gradleVersion = '3.2'
}
